plugins {
    id 'java-library'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'checkstyle'
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:15.0'
}

group = 'au.com.grieve'
version = './src/build/version.py'.execute().text.trim()
description = 'PortalNetwork'
java.sourceCompatibility = JavaVersion.VERSION_21
base {
    archivesName = "${description}-Clod"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:deprecation'
}

spotless {
    format 'misc', {
        target '*.gradle', '.gitattributes', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
    java {
        googleJavaFormat('1.19.2').reflowLongStrings().skipJavadocFormatting()
        formatAnnotations()
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

if (file('local.gradle').exists()) {
    apply from: 'local.gradle'
}
